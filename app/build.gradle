plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 34

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            universalApk false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId 'app.clauncher'
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 120
        versionName "v4.3.6"

        resourceConfigurations += ["en", "ar", "de", "es-rES", "es-rUS", "fr", "hr", "hu", "in", "it", "ja", "pl", "pt-rBR", "ru-rRU", "sv", "tr", "uk", "zh"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_PATH") ?: "${rootProject.projectDir}/release.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Enable reproducible builds
            buildConfigField "Long", "BUILD_TIME", "0L"
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }
    
    namespace 'app.clauncher'

    // Add reproducible build configurations
    aaptOptions {
        cruncherEnabled = false
    }

    // Enable reproducible build output
    dependenciesInfo {
        includeInApk = false
    }
}

// Add a task to sort the files in the APK
tasks.withType(com.android.build.gradle.tasks.PackageApplication).configureEach {
    zip64 = true
    doLast {
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                output.outputFile.withInputStream { fis ->
                    def sortedEntries = new java.util.zip.ZipInputStream(fis).withCloseable { zin ->
                        def entries = []
                        def entry
                        while ((entry = zin.nextEntry) != null) {
                            entries << entry.name
                        }
                        entries.sort()
                        return entries
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation libs.kotlin.stdlib
    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.recyclerview

    // Android lifecycle
    implementation libs.lifecycle.extensions
    implementation libs.lifecycle.viewmodel.ktx

    // Navigation
    implementation libs.navigation.fragment.ktx

    // Work Manager
    implementation libs.work.runtime.ktx

    //Material dependency
    implementation libs.material
}
